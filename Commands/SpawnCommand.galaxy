// Cortex SC2 Roleplaying Engine
// Copyright (C) 2009-2010 <http://www.cortexrp.com/>
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; version 2 of the License.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

void libcrtx_command_spawn_resetinstance()
{
	libcrtx_command_spawn_instances[EventPlayer()].unitType = "";
	libcrtx_command_spawn_instances[EventPlayer()].count = 1;
	libcrtx_command_spawn_instances[EventPlayer()].player = EventPlayer();
	libcrtx_command_spawn_instances[EventPlayer()].loc = null;
}

bool libcrtx_command_spawn_parse(string lp_message)
{
	bool first = true;
	string colour; // spelling this british-way since color is a reserved word
	int misc;
	string val;
	bool doneSpawnType = false;
	int currentParameter = 2;

	//libcrtx_debug("libcrtx_command_spawn_parse(" + lp_message + ")");
	//libcrtx_debug("param 2 is " + StringWord(lp_message, currentParameter) );
	while(StringWord(lp_message, currentParameter) != "")
	{
		if( first ) {
			first = false;
			libcrtx_command_spawn_resetinstance();
		}

		val = StringWord(lp_message, currentParameter);
		if( StringToInt(val) != 0 ) // integer - spawncount only
		{
			// reset when we have a new integer!
			libcrtx_command_spawn_instances[EventPlayer()].count = libcrtx_boundsInt(StringToInt(val), 0, libcrtx_setting_getint(libcrtx_setting_int_maxspawncount));
			
			//libcrtx_debug("caught integer" + IntToString(libcrtx_command_spawn_instances[EventPlayer()].count));
			//libcrtx_debug("max spawn count " + IntToString(libcrtx_setting_getint(libcrtx_setting_int_maxspawncount)) );
		}
		else if( StringSub(val, 1, 1) == "@") // we're redirecting ownership
		{
			misc = StringLength(val);
			// expected param: @red, @blue, @hostile, etc.
			colour = StringSub(val, 2, misc);
			// TODO: map colors to player numbers.
			misc = libcrtx_utility_getplayerfromcolor(colour);
			if( misc != -1 ) {
				libcrtx_command_spawn_instances[EventPlayer()].player = misc;
			}
			libcrtx_debug("caught ownership redirect " + IntToString(misc));
		}
		else if( StringSub(val, 1, 1) == "#") // we're redirecting the location
		{
			misc = StringLength(val);
			libcrtx_command_spawn_instances[EventPlayer()].loc = DataTableGetRegion(true, libcrtx_command_region_buildidentifier( StringSub(val, 2, misc) ) );
		}
		else // the only remainder isssssss, spawn type!
		{
			if(doneSpawnType)
			{
				// append it on.
				libcrtx_command_spawn_instances[EventPlayer()].unitType = libcrtx_command_spawn_instances[EventPlayer()].unitType + val;
			}
			else
			{

				doneSpawnType = true;
				libcrtx_command_spawn_instances[EventPlayer()].unitType = val;
				//libcrtx_debug("caught spawn type");
			}
		}
		
		
		currentParameter = currentParameter + 1;
	}

	return true;
}

void libcrtx_command_spawn_execute()
{
	string uType;
	unit u;
	point p;
	if( libcrtx_command_spawn_instances[EventPlayer()].count == 0 ) {
		return; // count == 0, don't bother.
	}

	u = libcrtx_utility_getspawner();
	p = UnitGetPosition(u);
	if( libcrtx_command_spawn_instances[EventPlayer()].loc ) {
		p = RegionGetCenter(libcrtx_command_spawn_instances[EventPlayer()].loc);
	}
	

	uType = UnitTypeFromString(libcrtx_command_spawn_instances[EventPlayer()].unitType);
	if( uType == "" || uType == libcrtx_getspawnertype() || libcrtx_toolkit_isrestrictedunit(uType) )
	{
		return;
	}

	libcrtx_command_spawn_count[EventPlayer()] = libcrtx_command_spawn_count[EventPlayer()] + libcrtx_command_spawn_instances[EventPlayer()].count;
	if( TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]) - libcrtx_command_spawn_lastevent[EventPlayer()] > libcrtx_setting_getint(libcrtx_setting_int_spawnperiod) ) {
		libcrtx_command_spawn_lastevent[EventPlayer()] = FixedToInt(TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]));
	}

	// Frozen command.
	if( libcrtx_command_spawn_frozenuntil[EventPlayer()] > TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]) ) {
		return;
	}
	
	//libcrtx_debug("creating " + IntToString(libcrtx_command_spawn_instances[EventPlayer()].count) + " " + libcrtx_command_spawn_instances[EventPlayer()].unitType);
	UnitCreate( libcrtx_command_spawn_instances[EventPlayer()].count, uType, c_unitCreateIgnorePlacement, libcrtx_command_spawn_instances[EventPlayer()].player, p, 270.0);
}

bool libcrtx_command_spawn(bool testConds, bool runActions)
{
	string message;

	//libcrtx_debug("libcrtx_command_spawn()");

	if( !runActions ) {
		return true;
	}

	// Spawn Throttle reset check
	if( TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]) - libcrtx_command_spawn_lastevent[EventPlayer()] > libcrtx_setting_getint(libcrtx_setting_int_spawnperiod) ) {
		libcrtx_command_spawn_count[EventPlayer()] = 0;
	}

	// Can we still spawn units? (Spawn Throttle)
	if( libcrtx_admin_getpermissions(EventPlayer()) < libcrtx_admin_permissionlevel_moderator && (libcrtx_command_spawn_count[EventPlayer()] >= libcrtx_setting_getint(libcrtx_setting_int_maxspawnrate) || libcrtx_command_spawn_frozenuntil[EventPlayer()] > TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]))) {
		libcrtx_writelocalerror("You cannot spawn that many units so quickly. Please wait a while, and try again.");
		libcrtx_writetext(PlayerGroupAll(), libcrtx_utility_coloredplayername(EventPlayer()) + StringToText(" is attempting to spawn in excess of " + IntToString(libcrtx_setting_getint(libcrtx_setting_int_maxspawnrate)) + " units in " + IntToString(libcrtx_setting_getint(libcrtx_setting_int_spawnperiod)) + " seconds."));
		if( libcrtx_command_spawn_frozenuntil[EventPlayer()] < TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]) ) {
			libcrtx_command_spawn_frozenuntil[EventPlayer()] = TimerGetElapsed(libNtve_gv__GameTimer[EventPlayer()]) + libcrtx_setting_getint(libcrtx_setting_int_spawntimeout);
		}
		// Do not halt execution, move on and allow the command to parse so it can be repeated using @spawn later.
	}

	message = libcrtx_chat_get_parse_line(EventPlayer());
	if( libcrtx_command_spawn_parse(message) )
	{
		libcrtx_command_spawn_execute();
	}
	else
	{
		//libcrtx_debug("invalid syntax.");
	}

	return true;
}