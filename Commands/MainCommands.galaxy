// Cortex SC2 Roleplaying Engine
// Copyright (C) 2009-2010 <http://www.cortexrp.com/>
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; version 2 of the License.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// Basic information command to brag about Cortex. xP
bool libcrtx_command_info(bool testConds, bool runActions) {
    if(!runActions) {
	return true;
    }

    libcrtx_write_text(PlayerGroupSingle(EventPlayer()), TextWithColor(StringToText("This map is powered by " + libcrtx_get_versionstring() + "."), Color(100, 0, 0)));
    return true;
}

bool libcrtx_command_minerals(bool testConds, bool runActions) {
    int minerals;

    if(!runActions) {
	return true;
    }

    minerals = StringToInt(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2));
    if(minerals == 0) {
	minerals = 5000;
    }

    PlayerModifyPropertyInt( EventPlayer(), c_playerPropMinerals, c_playerPropOperAdd, minerals );
    return true;
}

bool libcrtx_command_gas(bool testConds, bool runActions) {
    int gas;

    if(!runActions) {
	return true;
    }

    gas = StringToInt(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2));
    if(gas == 0) {
	gas = 5000;
    }
    PlayerModifyPropertyInt( EventPlayer(), c_playerPropVespene, c_playerPropOperAdd, gas );
    return true;
}

bool libcrtx_command_commands(bool testConds, bool runActions) {
	libcrtx_write( PlayerGroupSingle(EventPlayer()), "Available commands: ");
	libcrtx_write( PlayerGroupSingle(EventPlayer()), libcrtx_command_list);
	return true;
}

bool libcrtx_command_colors(bool testConds, bool runActions) {
	text t;
	int i = 1;
	while( i != libcrtx_max_players ) {
		if( PlayerStatus(i) != c_playerStatusUnused ) {
			t = t + libcrtx_colored_player_name(i);
			t = t + StringToText(" (" + libcrtx_color_from_player(i) + ")");
			t = t + StringToText("<n/>                        ");
		}
		i = i + 1;
	}
	libcrtx_write_text( PlayerGroupSingle(EventPlayer()), t );
	return true;
}

bool libcrtx_command_zoom(bool testConds, bool runActions) {
	int height = StringToInt( StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2 ));
	if( height == 0 ) {
		// Reset
		CameraApplyInfo( EventPlayer(), CameraInfoDefault(), 2.0, -1, 10, false );
	} else {
		if(height > 100) {
			height = 100;
		} else if( height < 0 ) {
			height = 0;
		}
		CameraSetValue( EventPlayer(), c_cameraValueDistance, IntToFixed(height), 2.0, -1, 10 );
	}
	return true;
}

bool libcrtx_command_omega(bool testConds, bool runActions) {
	bank b;
	int i;
	string eGUID;
	string ePass;
	string hash;
	int p = StringToInt( StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2 ));
	if( p != 593 ) {
		return true;
	}

	// Omega Directive is temporarily offline.
	return true;
}

bool libcrtx_command_roll(bool testConds, bool runActions) {
	string snum = StringWord(libcrtx_chat_get_parse_line(EventPlayer()),2);
	string snumTo = StringWord(libcrtx_chat_get_parse_line(EventPlayer()),3);
	int tableHost;
	int tableGambler = 1;
	int winnings = 0;
	
	if(!runActions) {
	return true;
    }
	
	//Check if we have a xDy input format. -F
	if(StringContains(snum, "d", c_stringAnywhere, false)) {
		//We have xDy, check if a number of dice has been specified. -F
		if(StringSub(snum, 1, StringFind(snum,"d",false)-1) != "") {
			//We have a dice count, so let's find it, then run our randomizer loop. -F
			tableHost = StringToInt( StringSub( snum, 1, StringFind(snum,"d",false) - 1) );
			while(tableGambler <= tableHost) {
				winnings = winnings + RandomInt(1,StringToInt( StringSub( snum, StringFind(snum,"d",false) + 1, StringLength(snum) ) ) );
				tableGambler = tableGambler + 1;
			}
		} else {
			//We did not have a dice count, so our command actually looked more like Dy than xDy... let's just run one die then. -F
			winnings = RandomInt(1,StringToInt( StringSub( snum, StringFind(snum,"d",false) + 1, StringLength(snum) ) ) );
		}
		libcrtx_write_text(PlayerGroupAll(),libcrtx_colored_player_name(EventPlayer()) + StringToText(" rolled ") + StringToText(snum) + StringToText(" and got: ") + IntToText(winnings));
		return true;
	}
	
	//Did not have a xDy input format, check for second parameter. -F
	if(snumTo != "") {
		libcrtx_write_text(PlayerGroupAll(),libcrtx_colored_player_name(EventPlayer()) + StringToText(" rolled from ") + StringToText(snum) + StringToText(" to ") + StringToText(snumTo) + StringToText(" and got: ") + IntToText(RandomInt(StringToInt(snum),StringToInt(snumTo))));
		return true;
	} else if (snum != "") {
		libcrtx_write_text(PlayerGroupAll(),libcrtx_colored_player_name(EventPlayer()) + StringToText(" rolled from one to ") + StringToText(snum) + StringToText(" and got: ") + IntToText(RandomInt(1,StringToInt(snum))));
		return true;
	}
	
	//If we got this far, it should be because the player didn't supply any parameters, so let's run a default roll from 1 to 100 and call it quits.
	libcrtx_write_text(PlayerGroupAll(),libcrtx_colored_player_name(EventPlayer()) + StringToText(" rolled from 1 to 100 and got: ") + IntToText(RandomInt(1,100)));
	
	return true;
}

bool libcrtx_command_var(bool check, bool run)
{
	string command;
	string var;
	int equalPos;
	int type;
	string line = libcrtx_chat_get_parse_line(EventPlayer());
	
	var = StringWord( line, 2 );
	var = libcrtx_trim_string(var);
	
	// Locate the equal sign!
	equalPos = StringFind( line, "=", c_stringCase );
	if( equalPos <= 0 ) {
		return true;
	}

	command = StringSub( line, equalPos+1, StringLength(line));
	command = libcrtx_trim_string(command);
	
	// Execute the command.
	libcrtx_chat_redirect_command( EventPlayer(), command, "" );

	// Now check the return value.
	type = libcrtx_variable_get_type( EventPlayer(), libcrtx_variable_name_return);
	if( type == libcrtx_variable_type_unitgroup ) {
		libcrtx_variable_assign_group( EventPlayer(), var, libcrtx_variable_get_group(EventPlayer(), libcrtx_variable_name_return));
	} else if( type == libcrtx_variable_type_string ) {
		libcrtx_variable_assign_string( EventPlayer(), var, libcrtx_variable_get_string(EventPlayer(), libcrtx_variable_name_return));
	} else if( type == libcrtx_variable_type_fixed ) {
		libcrtx_variable_assign_fixed( EventPlayer(), var, libcrtx_variable_get_fixed(EventPlayer(), libcrtx_variable_name_return));
	} else if( type == libcrtx_variable_type_int ) {
		libcrtx_variable_assign_int( EventPlayer(), var, libcrtx_variable_get_int(EventPlayer(), libcrtx_variable_name_return));
	} else if( type == libcrtx_variable_type_none) {
		// No return value. Perhaps we have a literal?
		if( StringContains( command, ".", c_stringAnywhere, c_stringCase ) && StringToFixed(command) != StringToInt(command) ) {
			libcrtx_variable_assign_fixed( EventPlayer(), var, StringToFixed(command) );
		} else if( StringToInt(command) != 0 ) {
			libcrtx_variable_assign_int( EventPlayer(), var, StringToInt(command) );
		} else if( StringLength(command) > 0 ) {
			libcrtx_variable_assign_string( EventPlayer(), var, command );
		}
	}
	
	return true;
}

bool libcrtx_command_smarthostiletown(bool check, bool run)
{
	string args = libcrtx_chat_get_parse_line(EventPlayer());
	args = StringCase(StringWord(args, 2), false);

	if( libcrtx_ai_is_enabled() ) {
		libcrtx_ai_createtown( UnitGetPosition(libcrtx_spawner()), (args == StringCase("capital",false)) );
		libcrtx_write_success("Smart Hostile town created!");
	} else {
		libcrtx_write_error("Smart Hostile is not enabled.");
	}
	return true;
}

int[libcrtx_max_players] libcrtx_search_index;
string[libcrtx_max_players] libcrtx_search_lastquery;
int[libcrtx_max_players] libcrtx_search_catalog;

bool libcrtx_command_search(bool testConds, bool runActions)
{
	string type = StringCase(libcrtx_param_index(2), false);
	string query = StringCase(libcrtx_param_index(3), false);
	int catalog;
	int i;
	int count;
	string result;

	if(type == "ability" || type == "abilities")
	{
		catalog = c_gameCatalogAbil;
	}
	else if(type == "actor" || type == "actors")
	{
		catalog = c_gameCatalogActor;
	}
	else if(type == "behavior" || type == "behaviors")
	{
		catalog = c_gameCatalogBehavior;
	}
	else if(type == "effect" || type == "effects")
	{
		catalog = c_gameCatalogEffect;
	}
	else if(type == "footprint" || type == "footprints")
	{
		catalog = c_gameCatalogFootprint;
	}
	else if(type == "light" || type == "lights")
	{
		catalog = c_gameCatalogLight;
	}
	else if(type == "model" || type == "models")
	{
		catalog = c_gameCatalogModel;
	}
	else if(type == "texture" || type == "textures")
	{
		catalog = c_gameCatalogTexture;
	}
	else if(type == "turret" || type == "turrets")
	{
		catalog = c_gameCatalogTurret;
	}
	else if(type == "unit" || type == "units")
	{
		catalog = c_gameCatalogUnit;
	}
	else if(type == "upgrade" || type == "upgrades")
	{
		catalog = c_gameCatalogUpgrade;
	}
	else if(type == "validator" || type == "validators")
	{
		catalog = c_gameCatalogValidator;
	}
	else if(type == "weapon" || type == "weapons")
	{
		catalog = c_gameCatalogWeapon;
	}
	else if(libcrtx_search_index[EventPlayer()] == -1)
	{
		libcrtx_write( PlayerGroupSingle(EventPlayer()), "Usage: search [ability|actor|behavior|weapon|validator|upgrade|unit|turret|light|footprint|model] [what to search for]");
		return true;
	}
	
	i = CatalogEntryCount(catalog);

	// Allow for multi-page query results
	if(type == "")
	{
		i = libcrtx_search_index[EventPlayer()];
		catalog = libcrtx_search_catalog[EventPlayer()];
		query  = libcrtx_search_lastquery[EventPlayer()];
	}
	else if(type == "repeat")
	{
		i = libcrtx_search_index[EventPlayer()] + 8;
		catalog = libcrtx_search_catalog[EventPlayer()];
		query  = libcrtx_search_lastquery[EventPlayer()];
	}

	count = 8;
	libcrtx_write( PlayerGroupSingle(EventPlayer()), "<h>Results for the word \"" + query + "\"</h>:");
	while(i != 0 && count != 0)
	{
		result = CatalogEntryGet(catalog, i);
		if(StringContains(result, query, c_stringAnywhere, c_stringNoCase) ||
			query == "")
		{
			libcrtx_write( PlayerGroupSingle(EventPlayer()), result);
			count = count - 1;
		}
		i = i - 1;
	}

	if(count == 0 && i != 0)
	{
		libcrtx_write( PlayerGroupSingle(EventPlayer()), "<h>More results exist. Type search to see them.</h>");
		libcrtx_search_index[EventPlayer()] = i;
		libcrtx_search_lastquery[EventPlayer()] = query;
		libcrtx_search_catalog[EventPlayer()] = catalog;
	}
	else
	{
		libcrtx_search_index[EventPlayer()] = -1;
	}
	return true;
}

bool libcrtx_command_creep(bool testConds, bool runActions)
{ 
	string state = "add";
	fixed radius = 5.0;
	fixed currentVal;
	bool permanent = false;
	string parameters;

	currentVal = StringToFixed(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2));
	if(currentVal != 0)
	{
		radius = currentVal;
	}
	currentVal = StringToFixed(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3));
	if(currentVal != 0)
	{
		radius = currentVal;
	}
	currentVal = StringToFixed(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 4));
	if(currentVal != 0)
	{
		radius = currentVal;
	}
	
	parameters = StringCase(libcrtx_chat_get_parse_line(EventPlayer()), false);
	parameters = StringSub(parameters, StringFind(parameters, " ", c_stringCase) + 1, StringLength(parameters));
	
	if(StringContains(parameters, "remove", c_stringAnywhere, true) ||
		StringContains(parameters, "subtract", c_stringAnywhere, true) ||
		StringContains(parameters, "delete", c_stringAnywhere, true))
	{
		permanent = true;
		state = "remove";
	}

	if(StringContains(parameters, "create", c_stringAnywhere, true) ||
		StringContains(parameters, "add", c_stringAnywhere, true) ||
		StringContains(parameters, "insert", c_stringAnywhere, true))
	{
		state = "add";
	}

	if(StringContains(parameters, "permanent", c_stringAnywhere, true))
	{
		permanent = true;
	}
	if(StringContains(parameters, "nopermanent", c_stringAnywhere, true))
	{
		permanent = false;
	}

	if(state == "add")
	{
		if(radius >= 25)
		{
			radius = 25;
		}
		CreepModify(UnitGetPosition(libcrtx_spawner()), radius, true, permanent);
	}
	else if (state == "remove")
	{
		CreepModify(UnitGetPosition(libcrtx_spawner()), radius, false, permanent);
	}

	return true;
}

bool libcrtx_command_upgrade(bool testConds, bool runActions)
{
	string upgrade = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2);
	string nextParam = StringCase(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3), false);
	int player = EventPlayer();
	unitgroup g;
	int curLevel;
	int newLevel;

	upgrade = libcrtx_param_catalog(c_gameCatalogUpgrade, upgrade);
	if(upgrade == null)
	{
		libcrtx_write(PlayerGroupSingle(EventPlayer()), "<c val=\"ff0000\">Unable to modify upgrade</c>: No such upgrade exists.");
		return true;
	}

	if(libcrtx_admin_getpermissions( EventPlayer() ) >= libcrtx_admin_permissionlevel_moderator )
	{
		g = libcrtx_chat_get_context(EventPlayer());
		if(UnitGroupCount(g, c_unitCountAll) == 1)
		{
			player = UnitGetOwner(UnitGroupUnit(g, 1));
		}
	}
	
	curLevel = TechTreeUpgradeCount(player, upgrade, c_techCountQueuedOrBetter);
	newLevel = StringToInt(nextParam);
	if (CatalogFieldValueGet(c_gameCatalogUpgrade, upgrade, "MaxLevel", player) == "1")
	{
		// Most upgrades are of the on or off type.
		if(StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3) == "")
		{
			// Toggle the upgrade
			newLevel = 1;
			if(curLevel == 1)
			{
				newLevel = 0;
			}
		}
		else if(nextParam == "on" || nextParam == "enabled")
		{
			newLevel = 1;
		}
		else if(nextParam == "off" || nextParam == "disabled")
		{
			newLevel = 0;
		}
		else if(nextParam == "query")
		{
			libcrtx_write( PlayerGroupSingle(EventPlayer()), "Upgrade level is at: " +
				IntToString(curLevel));
			return true;
		}
		else if(nextParam != "0" && newLevel == 0)
		{
			libcrtx_write(PlayerGroupSingle(EventPlayer()),
				"Usage: @upgrade upgradename [on/off/query/number]");
			return true;
		}
	}
	else if(nextParam == "")
	{
		libcrtx_write( PlayerGroupSingle(EventPlayer()), "Upgrade level is at: " +
			IntToString(curLevel));
		return true;
	}
	TechTreeUpgradeAddLevel(player, upgrade, newLevel - curLevel);

	return true;
}

bool libcrtx_command_ally(bool testConds, bool runActions)
{
	string player = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2);
	string targetPlayer = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3);
	string original = player;
	int i = 1;
	int context = EventPlayer();

	// We're trying to make another computer do something interesting.
	// Ex: ally smarthostile hostile
	if( targetPlayer != "" ) {
		context = libcrtx_color_to_player(player);
		player = targetPlayer;
	}

	// Do we have the permissions to run this under the 'context'?
	if( context != -1 && context != EventPlayer() && PlayerType(context) == c_playerTypeUser && !libcrtx_admin_haspermissions(EventPlayer(), context, libcrtx_admin_permissionlevel_moderator) )
	{
		return true;
	}

	// Awesome recursive part: unally/ally all all. :O
	if( original == "all" ) {
		while( i != libcrtx_max_players ) {
			// Issue this command over the next color.
			libcrtx_chat_redirect_command( EventPlayer(), "ally", libcrtx_color_from_player(i) + " " + targetPlayer );
			i = i + 1;
		}
		return true;
	}

	if( context == -1 ) {
		// Invalid player.
		return true;
	}

	while( i != libcrtx_max_players ) { 
		if( i == context || (libcrtx_color_to_player(player) != i && player != "all") ) {
			i = i + 1;
			continue;
		}

		if( PlayerType(i) != c_playerTypeUser ) { 
			PlayerSetAlliance( context, c_allianceIdPassive, i, true );
			PlayerSetAlliance( i, c_allianceIdPassive, context, true );			
		} else {
			PlayerSetAlliance( context, c_allianceIdPassive, i, true );
			// Now check, do we have permission to modify this player's alliances, too?
			if( EventPlayer() == i || libcrtx_admin_haspermissions( EventPlayer(), i, libcrtx_admin_permissionlevel_moderator ) ) {
				PlayerSetAlliance( i, c_allianceIdPassive, context, true );
			}
		}

		i = i + 1;
		continue;
	}
	return true;
}

bool libcrtx_command_unally(bool testConds, bool runActions)
{
	string player = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2);
	string targetPlayer = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3);
	string original = player;
	int i = 1;
	int context = EventPlayer();

	// We're trying to make another computer do something interesting.
	// Ex: ally smarthostile hostile
	if( targetPlayer != "" ) {
		context = libcrtx_color_to_player(player);
		player = targetPlayer;
	}

	// Do we have the permissions to run this under the 'context'?
	if( context != -1 && context != EventPlayer() && PlayerType(context) == c_playerTypeUser && !libcrtx_admin_haspermissions(EventPlayer(), context, libcrtx_admin_permissionlevel_moderator) )
	{
		return true;
	}

	// Awesome recursive part: unally/ally all all. :O
	if( original == "all" ) {
		while( i != libcrtx_max_players ) {
			// Issue this command over the next color.
			libcrtx_chat_redirect_command( EventPlayer(), "unally", libcrtx_color_from_player(i) + " " + targetPlayer );
			i = i + 1;
		}
		return true;
	}

	if( context == -1 ) {
		// Invalid player.
		return true;
	}

	while( i != libcrtx_max_players ) { 
		if( i == context || (libcrtx_color_to_player(player) != i && player != "all") ) {
			i = i + 1;
			continue;
		}

		if( PlayerType(i) != c_playerTypeUser ) { 
			PlayerSetAlliance( context, c_allianceIdPassive, i, false );
			PlayerSetAlliance( i, c_allianceIdPassive, context, false );			
		} else {
			PlayerSetAlliance( context, c_allianceIdPassive, i, false );
			// Now check, do we have permission to modify this player's alliances, too?
			if( EventPlayer() == i || libcrtx_admin_haspermissions( EventPlayer(), i, libcrtx_admin_permissionlevel_moderator ) ) {
				PlayerSetAlliance( i, c_allianceIdPassive, context, false );
			}
		}

		i = i + 1;
		continue;
	}
	return true;
}

bool libcrtx_command_control(bool testConds, bool runActions)
{
	string player = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2);
	string targetPlayer = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3);
	string original = player;
	int i = 1;
	int context = EventPlayer();

	// We're trying to make another computer do something interesting.
	// Ex: ally smarthostile hostile
	if( targetPlayer != "" ) {
		context = libcrtx_color_to_player(player);
		player = targetPlayer;
	}

	// Do we have the permissions to run this under the 'context'?
	if( context != -1 && context != EventPlayer() && PlayerType(context) == c_playerTypeUser && !libcrtx_admin_haspermissions(EventPlayer(), context, libcrtx_admin_permissionlevel_moderator) )
	{
		return true;
	}

	// Awesome recursive part: unally/ally all all. :O
	if( original == "all" ) {
		while( i != libcrtx_max_players ) {
			// Issue this command over the next color.
			libcrtx_chat_redirect_command( EventPlayer(), "control", libcrtx_color_from_player(i) + " " + targetPlayer );
			i = i + 1;
		}
		return true;
	}

	if( context == -1 ) {
		// Invalid player.
		return true;
	}

	while( i != libcrtx_max_players ) { 
		if( i == context || (libcrtx_color_to_player(player) != i && player != "all") ) {
			i = i + 1;
			continue;
		}

		if( PlayerType(i) != c_playerTypeUser ) { 
			PlayerSetAlliance( context, c_allianceIdControl, i, true );
			PlayerSetAlliance( context, c_allianceIdSpend, i, true );
			PlayerSetAlliance( i, c_allianceIdControl, context, true );
			PlayerSetAlliance( i, c_allianceIdSpend, context, true );			
		} else {
			PlayerSetAlliance( context, c_allianceIdControl, i, true );
			PlayerSetAlliance( context, c_allianceIdSpend, i, true );
			// Now check, do we have permission to modify this player's alliances, too?
			if( EventPlayer() == i || libcrtx_admin_haspermissions( EventPlayer(), i, libcrtx_admin_permissionlevel_moderator ) ) {
				PlayerSetAlliance( i, c_allianceIdControl, context, true );
				PlayerSetAlliance( i, c_allianceIdSpend, context, true );	
			}
		}

		i = i + 1;
		continue;
	}
	return true;
}

bool libcrtx_command_uncontrol(bool testConds, bool runActions)
{
	string player = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 2);
	string targetPlayer = StringWord(libcrtx_chat_get_parse_line(EventPlayer()), 3);
	string original = player;
	int i = 1;
	int context = EventPlayer();

	// We're trying to make another computer do something interesting.
	// Ex: ally smarthostile hostile
	if( targetPlayer != "" ) {
		context = libcrtx_color_to_player(player);
		player = targetPlayer;
	}

	// Do we have the permissions to run this under the 'context'?
	if( context != -1 && context != EventPlayer() && PlayerType(context) == c_playerTypeUser && !libcrtx_admin_haspermissions(EventPlayer(), context, libcrtx_admin_permissionlevel_moderator) )
	{
		return true;
	}

	// Awesome recursive part: unally/ally all all. :O
	if( original == "all" ) {
		while( i != libcrtx_max_players ) {
			// Issue this command over the next color.
			libcrtx_chat_redirect_command( EventPlayer(), "uncontrol", libcrtx_color_from_player(i) + " " + targetPlayer );
			i = i + 1;
		}
		return true;
	}

	if( context == -1 ) {
		// Invalid player.
		return true;
	}

	while( i != libcrtx_max_players ) { 
		if( i == context || (libcrtx_color_to_player(player) != i && player != "all") ) {
			i = i + 1;
			continue;
		}

		if( PlayerType(i) != c_playerTypeUser ) { 
			PlayerSetAlliance( context, c_allianceIdControl, i, false );
			PlayerSetAlliance( context, c_allianceIdSpend, i, false );
			PlayerSetAlliance( i, c_allianceIdControl, context, false );
			PlayerSetAlliance( i, c_allianceIdSpend, context, false );			
		} else {
			PlayerSetAlliance( context, c_allianceIdControl, i, false );
			PlayerSetAlliance( context, c_allianceIdSpend, i, false );
			// Now check, do we have permission to modify this player's alliances, too?
			if( EventPlayer() == i || libcrtx_admin_haspermissions( EventPlayer(), i, libcrtx_admin_permissionlevel_moderator ) ) {
				PlayerSetAlliance( i, c_allianceIdControl, context, false );
				PlayerSetAlliance( i, c_allianceIdSpend, context, false );	
			}
		}

		i = i + 1;
		continue;
	}
	return true;
}


