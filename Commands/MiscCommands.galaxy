// Cortex SC2 Roleplaying Engine
// Copyright (C) 2009-2010 <http://www.cortexrp.com/>
//
// $Id: InfoCommand.galaxy 2 2010-04-25 01:35:09Z TheBlackMage $
// 
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; version 2 of the License.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

bool libcrtx_command_kill(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitKill( UnitGroupLoopCurrent() );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_remove(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitRemove( UnitGroupLoopCurrent() );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_removearea(bool testConds, bool runActions) {
      unitgroup g;
      fixed radius = StringToFixed( StringWord( EventChatMessage(false), 2 ));
      if(radius == 0.0) {
        radius = 5.0;
      }

	g = UnitGroup( null, c_playerAny, RegionCircle( UnitGetPosition(libcrtx_utility_getspawner()), radius), null, 0 );
      g = libcrtx_utility_enforce_unit_permissions(g);

	UnitGroupLoopBegin(g);

	while(!UnitGroupLoopDone())

	{

		UnitRemove( UnitGroupLoopCurrent() );

		UnitGroupLoopStep();

	}

	UnitGroupLoopEnd();

	return true;

}

bool libcrtx_command_scale(bool testConds, bool runActions) {
	fixed scale;
	fixed scaleY;
	fixed scaleZ;
	unitgroup g = libcrtx_utility_getselectedunits();
	scale = StringToFixed(StringWord(EventChatMessage(false), 2));
	scaleY = StringToFixed(StringWord(EventChatMessage(false), 3));
	scaleZ = StringToFixed(StringWord(EventChatMessage(false), 4));
	
	if(scaleZ == 0.0) {
		scaleZ = scale;
	}
	
	if(scaleY == 0.0) {
		scaleY = scale;
	}
	
	// Verify bounds.
	scale = libcrtx_minFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_maxscale), scale );
	scale = libcrtx_maxFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_minscale), scale );
	
	scaleY = libcrtx_minFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_maxscale), scaleY );
	scaleY = libcrtx_maxFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_minscale), scaleY );
	
	scaleZ = libcrtx_minFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_maxscale), scaleZ );
	scaleZ = libcrtx_maxFixed( libcrtx_setting_getfixed(libcrtx_setting_fixed_minscale), scaleZ );

	// Translate to Blizzard SC2 system.
	scale = scale * 100;
	scaleY = scaleY * 100;
	scaleZ = scaleZ * 100;
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetScale( UnitGroupLoopCurrent(), scale, scaleY, scaleZ );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;	
}

bool libcrtx_command_give(bool testConds, bool runActions) {
	int newOwner;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );
	newOwner = libcrtx_utility_getplayerfromcolor(nextParam);
	if(newOwner == -1) { // invalid entry by user
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetOwner( UnitGroupLoopCurrent(), newOwner, true );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_givecolor(bool testConds, bool runActions) {
	int newOwner;
	int oldOwner;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );
	newOwner = libcrtx_utility_getplayerfromcolor(nextParam);
	if(newOwner == -1) { // invalid entry by user
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetOwner( UnitGroupLoopCurrent(), newOwner, false );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_color(bool testConds, bool runActions) {
	int newColour;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );
	newColour = libcrtx_utility_getplayerfromdefaultcolor(nextParam);
	if(newColour == -1) { // invalid entry by user
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetTeamColorIndex( UnitGroupLoopCurrent(), newColour );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_invulnerable(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetState( UnitGroupLoopCurrent(), c_unitStateInvulnerable, true );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}


bool libcrtx_command_vulnerable(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetState( UnitGroupLoopCurrent(), c_unitStateInvulnerable, false );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_pause(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetState( UnitGroupLoopCurrent(), c_unitStatePaused, true );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_unpause(bool testConds, bool runActions) {
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetState( UnitGroupLoopCurrent(), c_unitStatePaused, false );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}


bool libcrtx_command_movable(bool testConds, bool runActions) {
	string msg;
	bool enable;
	bool query;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();
	query = false;
	nextParam = StringWord( EventChatMessage(false), 2 );
	if( nextParam == "on" ) {
		enable = true;
	} else if( nextParam == "off" ) {
		enable = false; 
	} else {
		// print whether it is or isn't.
		query = true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		if( query ) {
			if( UnitTestState( UnitGroupLoopCurrent(), c_unitStateMoveSuppressed ) ) {
				libcrtx_write( PlayerGroupSingle(EventPlayer()), "This unit is not movable.");
			} else {
				libcrtx_write( PlayerGroupSingle(EventPlayer()), "This unit is movable.");
			}
		} else {
			UnitSetState( UnitGroupLoopCurrent(), c_unitStateMoveSuppressed, enable );
		}

		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_life(bool testConds, bool runActions) {
	string nextParam;
	fixed val;
	bool percent = false;
	int position = 0;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );

	if( StringContains(nextParam, "%", c_stringAnywhere, c_stringCase) ) {
		percent = true;
		nextParam = StringSub(nextParam, 1, StringLength(nextParam)-1);
	}

	val = StringToFixed(nextParam);

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		if(percent) {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropLifePercent, val );
		} else {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropLife, val );
		}
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_energy(bool testConds, bool runActions) {
	string nextParam;
	fixed val;
	bool percent = false;
	int position = 0;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );

	if( StringContains(nextParam, "%", c_stringAnywhere, c_stringCase) ) {
		percent = true;
		nextParam = StringSub(nextParam, 1, StringLength(nextParam)-1);
	}

	val = StringToFixed(nextParam);

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		if(percent) {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropEnergyPercent, val );
		} else {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropEnergy, val );
		}
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_shields(bool testConds, bool runActions) {
	string nextParam;
	fixed val;
	bool percent = false;
	int position = 0;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );

	if( StringContains(nextParam, "%", c_stringAnywhere, c_stringCase) ) {
		percent = true;
		nextParam = StringSub(nextParam, 1, StringLength(nextParam)-1);
	}

	val = StringToFixed(nextParam);

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		if(percent) {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropShieldsPercent, val );
		} else {
			UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropShields, val );
		}
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_tint(bool testConds, bool runActions) {
	string paramOne;
	string paramTwo;
	string paramThree;
	string paramFour;

	int red;
	int green;
	int blue;
	fixed alpha;

	string amsg;
	actorscope ascope;
	actor act;

	unitgroup g = libcrtx_utility_getselectedunits();

	paramOne = StringWord( EventChatMessage(false), 2 );
	paramTwo = StringWord( EventChatMessage(false), 3 );
	paramThree = StringWord( EventChatMessage(false), 4 );
	paramFour = StringWord( EventChatMessage(false), 5 );

	if( StringContains(paramOne, "%", c_stringAnywhere, false) ) {
		paramOne = StringSub(paramOne, 1, StringLength(paramOne)-1);
		red = FixedToInt( 255.0 * StringToFixed(paramOne)/100.0 );
	} else {
		red = StringToInt(paramOne);
	}
	red = libcrtx_boundsInt(red, 0, 255);
	
	if( StringContains(paramTwo, "%", c_stringAnywhere, false) ) {
		paramTwo = StringSub(paramTwo, 1, StringLength(paramTwo)-1);
		green = FixedToInt( 255.0 * StringToFixed(paramTwo)/100.0 );
	} else {
		green = StringToInt(paramTwo);
	}
	green = libcrtx_boundsInt(green, 0, 255);
	
	if( StringContains(paramThree, "%", c_stringAnywhere, false) ) {
		paramThree = StringSub(paramThree, 1, StringLength(paramThree)-1);
		blue = FixedToInt( 255.0 * StringToFixed(paramThree)/100.0 );
	} else {
		blue = StringToInt(paramThree);
	}
	blue = libcrtx_boundsInt(blue, 0, 255);
	
	if( StringContains(paramFour, "%", c_stringAnywhere, false) ) {
		paramFour = StringSub(paramFour, 1, StringLength(paramFour)-1);
		alpha = StringToFixed(paramFour)/100.0;
	} else {
		alpha = StringToFixed(paramFour);
	}
	alpha = libcrtx_boundsFixed(alpha, 0, 1);

	if( paramFour == "" ) { // Unspecified, default to 1.0
		alpha = 1.0;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		ActorSend(libNtve_gf_MainActorofUnit(UnitGroupLoopCurrent()), "SetTintColor " + IntToString(red) + "," + IntToString(green) + "," + IntToString(blue));
		ActorSend(libNtve_gf_MainActorofUnit(UnitGroupLoopCurrent()), "SetOpacity {" + FixedToString(alpha, c_fixedPrecisionAny) + "} {" + "0 }");
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_face(bool testConds, bool runActions) {
	fixed angleToFace;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();

	nextParam = StringWord( EventChatMessage(false), 2 );
	if( nextParam != "" ) {
		angleToFace = StringToFixed( nextParam );
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		// face spawner
		if( nextParam == "" ) {
			libNtve_gf_MakeUnitFacePoint( UnitGroupLoopCurrent(), UnitGetPosition(libcrtx_utility_getspawner()), 0.0 );
		} else {
			// face angle
			UnitSetFacing( UnitGroupLoopCurrent(), angleToFace, 0.0 );
			libcrtx_debug("making face " + FixedToString(angleToFace, c_fixedPrecisionAny));
		}
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_height(bool testConds, bool runActions) {
	fixed height;
	fixed time;
	string nextParam;
	unitgroup g = libcrtx_utility_getselectedunits();
	nextParam = StringWord( EventChatMessage(false), 2 );
	height = StringToFixed( nextParam );
	nextParam = StringWord( EventChatMessage(false), 3 );
	time = StringToFixed( nextParam );

	UnitGroupLoopBegin(g);
	while( !UnitGroupLoopDone() )
	{
		// set height
		UnitSetHeight( UnitGroupLoopCurrent(), height, time );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_addweapon(bool testConds, bool runActions) {
	string weapon;
	string turret;
	unitgroup g = libcrtx_utility_getselectedunits();
	weapon = StringWord( EventChatMessage(false), 2 );
	turret = StringWord( EventChatMessage(false), 3 );
	
	if( turret == "" ) {
		turret = "CTurret";
	}
	
	// use toolkit on weapon
	weapon = libcrtx_toolkit_getweapon(weapon);

	UnitGroupLoopBegin(g);
	while( !UnitGroupLoopDone() )
	{
		UnitWeaponAdd( UnitGroupLoopCurrent(), weapon, turret );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_removeweapon(bool testConds, bool runActions) {
	string weapon;
	unitgroup g = libcrtx_utility_getselectedunits();
	weapon = StringWord( EventChatMessage(false), 2 );

	// use toolkit on weapon
	weapon = libcrtx_toolkit_getweapon(weapon);
	
	UnitGroupLoopBegin(g);
	while( !UnitGroupLoopDone() )
	{
		UnitWeaponRemove( UnitGroupLoopCurrent(), weapon );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_maxlife(bool testConds, bool runActions) {
	fixed val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToFixed(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 ) { // Bad input!
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropLifeMax, val );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_maxenergy(bool testConds, bool runActions) {
	fixed val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToFixed(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 ) { // Bad input!
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropEnergyMax, val );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_maxshields(bool testConds, bool runActions) {
	fixed val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToFixed(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 ) { // Bad input!
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropShieldsMax, val );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_speed(bool testConds, bool runActions) {
	fixed val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToFixed(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 ) { // Bad input!
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropMovementSpeed, val );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_kills(bool testConds, bool runActions) {
	fixed val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToFixed(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 ) { // Bad input!
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitSetPropertyFixed( UnitGroupLoopCurrent(), c_unitPropKills, val );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_level(bool testConds, bool runActions) {
	int val;
      int vval;

	unitgroup g = libcrtx_utility_getselectedunits();

	val = StringToInt(StringWord( EventChatMessage(false), 2 ));

	if( val <= 0 || val > libcrtx_toolkit_getmaxlevel()) { // Bad input!
		return true;
	}


	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		vval = val;

            // game crashes otherwise. silly blizzard.
            if( !UnitHasBehavior(UnitGroupLoopCurrent(), libcrtx_toolkit_getlevelbehavior()) ) {
                UnitGroupLoopStep();
                continue;
            }

            if( UnitLevel(UnitGroupLoopCurrent()) >= vval ) {
                UnitGroupLoopStep();
                continue;
            }

            while( UnitLevel(UnitGroupLoopCurrent()) < vval ) {
                UnitCreateEffectUnit( UnitGroupLoopCurrent(), libcrtx_toolkit_getxpincrementbehavior(), UnitGroupLoopCurrent() );
            }
		UnitGroupLoopStep();
	}

	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_adddamage(bool testConds, bool runActions) {

	int val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringToInt(StringWord( EventChatMessage(false), 2 ));
	if(val > 4096) {
	   val = 4096;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
            if( val > 0 )
            {
                UnitBehaviorAdd( UnitGroupLoopCurrent(), libcrtx_toolkit_getdamagebehavior(), UnitGroupLoopCurrent(), val );
            }
            else
            {
                UnitBehaviorRemove( UnitGroupLoopCurrent(), libcrtx_toolkit_getdamagebehavior(), AbsI(val) );
            }
		UnitGroupLoopStep();
	}

	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_attach(bool testConds, bool runActions) {
	string actr;
      string apoint;
	unitgroup g = libcrtx_utility_getselectedunits();
	actr = StringWord( EventChatMessage(false), 2 );
      apoint = StringWord( EventChatMessage(false), 3 );

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
            libNtve_gf_AttachActorToUnit( UnitGroupLoopCurrent(), actr, apoint);
            UIDisplayMessage( PlayerGroupAll(), c_messageAreaDebug, ActorScopeGetText( ActorScopeFromUnit( UnitGroupLoopCurrent() ) ));
            UIDisplayMessage( PlayerGroupAll(), c_messageAreaDebug, ActorGetText( libNtve_gf_MainActorofUnit( UnitGroupLoopCurrent() ) ));
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_removebehavior(bool testConds, bool runActions) {
	string val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringWord( EventChatMessage(false), 2 );

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitBehaviorRemove( UnitGroupLoopCurrent(), val, 1 );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}

bool libcrtx_command_addbehavior(bool testConds, bool runActions) {
	string val;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringWord( EventChatMessage(false), 2 );

	if( libcrtx_toolkit_isrestrictedbehavior(val) ) {
		return true;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		UnitBehaviorAdd( UnitGroupLoopCurrent(), val, UnitGroupLoopCurrent(), 1 );
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}


bool[libcrtx_max_players] libcrtx_command_firstperson_setup;
bool[libcrtx_max_players] libcrtx_command_firstperson_keyW;
bool[libcrtx_max_players] libcrtx_command_firstperson_keyA;
bool[libcrtx_max_players] libcrtx_command_firstperson_keyS;
bool[libcrtx_max_players] libcrtx_command_firstperson_keyD;
unit[libcrtx_max_players] libcrtx_command_firstperson_unit;
bool[libcrtx_max_players] libcrtx_command_firstperson_inmotion;
point[libcrtx_max_players] libcrtx_command_firstperson_lastpos;

bool libcrtx_command_firstperson_keylistener(bool testConds, bool runActions)
{
	if( EventKeyPressed() == c_keyW ) {
		libcrtx_command_firstperson_keyW[EventPlayer()] = !libcrtx_command_firstperson_keyW[EventPlayer()];
	} else if( EventKeyPressed() == c_keyA ) {
		libcrtx_command_firstperson_keyA[EventPlayer()] = !libcrtx_command_firstperson_keyA[EventPlayer()];
	} else if( EventKeyPressed() == c_keyS ) {
		libcrtx_command_firstperson_keyS[EventPlayer()] = !libcrtx_command_firstperson_keyS[EventPlayer()];
	} else if( EventKeyPressed() == c_keyD ) {
		libcrtx_command_firstperson_keyD[EventPlayer()] = !libcrtx_command_firstperson_keyD[EventPlayer()];
	}

	if( libcrtx_command_firstperson_setup[0] == false ) {
		libcrtx_command_firstperson_setup[0] = true;
		TriggerExecute( TriggerCreate("libcrtx_command_firstperson_moveupdate"), false, false );
	}
	return true;
}

bool libcrtx_command_firstperson_moveupdate(bool testConds, bool runActions)
{
	fixed fDiff;
	fixed sDiff;
	point p;
	int i = 1;
	fixed itp = 0.5;
	fixed itr = 20;
	while(true)
	{
		i = 1;
		while( i != libcrtx_max_players )
		{
			if( libcrtx_command_firstperson_unit[i] == null ) {
				i = i + 1;
				continue;
			}
			itp = UnitGetPropertyFixed( libcrtx_command_firstperson_unit[i], c_unitPropMovementSpeed, c_unitPropCurrent );
			itp = itp * 0.1;
			fDiff = 0;
			sDiff = 0;
			if( libcrtx_command_firstperson_keyW[i] ) {
				fDiff = fDiff + itp;
			}
			if( libcrtx_command_firstperson_keyS[i] ) {
				fDiff = fDiff - itp;
			}
			if( libcrtx_command_firstperson_keyA[i] ) {
				if( libcrtx_command_firstperson_keyS[i] ) { 
					sDiff = sDiff - itr;
				} else {
					sDiff = sDiff + itr;
				}
			}
			if( libcrtx_command_firstperson_keyD[i] ) {
				if( libcrtx_command_firstperson_keyS[i] ) {
					sDiff = sDiff + itr;
				} else {
					sDiff = sDiff - itr;
				}
			}

			if( fDiff != 0.0 ) {
				if( !libcrtx_command_firstperson_inmotion[i] ) {
					libNtve_gf_PlayAnimation( libNtve_gf_MainActorofUnit(libcrtx_command_firstperson_unit[i]), "Walk", "Walk", c_animFlagPlayForever, 1 );
					libcrtx_command_firstperson_inmotion[i] = true;
				}
				p = PointWithOffsetPolar( UnitGetPosition(libcrtx_command_firstperson_unit[i]), fDiff, UnitGetFacing(libcrtx_command_firstperson_unit[i]) );
				UnitSetPosition( libcrtx_command_firstperson_unit[i], p, true );
			} else {
				libcrtx_command_firstperson_inmotion[i] = false;
				libNtve_gf_ClearAnimation( libNtve_gf_MainActorofUnit(libcrtx_command_firstperson_unit[i]), "Walk" );
			}

			if( sDiff != 0.0 ) {
				if( fDiff != 0.0 ) {
					UnitSetFacing( libcrtx_command_firstperson_unit[i], UnitGetFacing(libcrtx_command_firstperson_unit[i]) + sDiff, 0 );
				} else {
					UnitSetFacing( libcrtx_command_firstperson_unit[i], UnitGetFacing(libcrtx_command_firstperson_unit[i]) + sDiff, 0.5 );
				}
			}

			i = i + 1;
		}
		Wait(0.1, c_timeReal);
	}
	return true;
}

bool libcrtx_command_firstperson(bool testConds, bool runActions) {
	string r;
	trigger t;
	unit u;
	unitgroup g = libcrtx_utility_getselectedunits();
	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		u = UnitGroupLoopCurrent();
		libcrtx_command_firstperson_unit[EventPlayer()] = u;

		libNtve_gf_MakeUnitUncommandable( u, true );
		

		UISetMode( PlayerGroupSingle(EventPlayer()), c_uiModeLetterboxed, 1.0);
		UISetFrameVisible( PlayerGroupSingle(EventPlayer()), c_syncFrameTypeHeroPanel, false );
		CameraLookAtUnit(EventPlayer(), u);
		CameraUseHeightSmoothing(EventPlayer(), true);
		CameraUseHeightDisplacement(EventPlayer(), true);
		CameraSetValue(EventPlayer(), c_cameraValuePitch, 20, 2.0, -1, 10);
		CameraSetValue(EventPlayer(), c_cameraValueDistance, 3, 1.0, -1, 10);
		CameraForceMouseRelative(EventPlayer(), true);
		CameraSetMouseRotates( EventPlayer(), true );
		libNtve_gf_DisplayBossBar( EventPlayer(), CatalogFieldValueGet(c_gameCatalogActor, UnitGetType(u), "UnitIcon", EventPlayer()), UnitTypeGetName( UnitGetType(u) ), 1, PlayerGroupSingle(EventPlayer()));
		libNtve_gf_SetBossBarBoss( EventPlayer(), UnitGroupLoopCurrent(), true );
		r = CatalogFieldValueGet(c_gameCatalogUnit, UnitGetType(u), "Race", EventPlayer());
		if( StringEqual(r, "Terr", false) ) {
			libNtve_gf_SetBossBarRace( EventPlayer(), 0, true );
		} else if( StringEqual(r, "Zerg", false) ) {
			libNtve_gf_SetBossBarRace( EventPlayer(), 2, true );
		} else {
			libNtve_gf_SetBossBarRace( EventPlayer(), 1, true );
		}
		libNtve_gf_MoveBossBar( EventPlayer(), c_anchorBottom, 0, 50);
		libNtve_gf_ShowHideBossBar( true, EventPlayer() );

		// Setup key listener
		if( !libcrtx_command_firstperson_setup[EventPlayer()] ) {
			libcrtx_command_firstperson_setup[EventPlayer()] = true;
			libcrtx_command_firstperson_lastpos[EventPlayer()] = UnitGetPosition(u);
			t = TriggerCreate("libcrtx_command_firstperson_keylistener");
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyW, true, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyA, true, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyS, true, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyD, true, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyW, false, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyA, false, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyS, false, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
			TriggerAddEventKeyPressed( t, EventPlayer(), c_keyD, false, c_keyModifierStateIgnore, c_keyModifierStateIgnore, c_keyModifierStateIgnore );
		}

		UnitGroupLoopEnd();
		
		while(u != null) {
			if( libcrtx_command_firstperson_inmotion[EventPlayer()] || DistanceBetweenPoints(libcrtx_command_firstperson_lastpos[EventPlayer()], UnitGetPosition(libcrtx_command_firstperson_unit[EventPlayer()])) > 0.5 ) { 
				CameraPan( EventPlayer(), UnitGetPosition(u), 0.1, -1, 10, false );
				libcrtx_command_firstperson_lastpos[EventPlayer()] = UnitGetPosition(libcrtx_command_firstperson_unit[EventPlayer()]);
			}
			Wait(0.3, c_timeReal);
		}
		break;
	}
	// No end unit loop.
	return true;
}

bool libcrtx_command_thirdperson(bool testConds, bool runActions) {
	unit u = libcrtx_command_firstperson_unit[EventPlayer()];
	UISetMode( PlayerGroupSingle(EventPlayer()), c_uiModeConsole, 1.0);
	if(u) { 
		libNtve_gf_MakeUnitUncommandable( u, false );
	}
	libcrtx_command_firstperson_unit[EventPlayer()] = null;
	CameraLookAtUnit(EventPlayer(), null);
	CameraUseHeightSmoothing(EventPlayer(), false);
	CameraForceMouseRelative(EventPlayer(), false);
	CameraSetMouseRotates(EventPlayer(), false);
	UISetFrameVisible( PlayerGroupSingle(EventPlayer()), c_syncFrameTypeHeroPanel, true );
	libNtve_gf_ShowHideBossBar( false, EventPlayer() );
	CameraApplyInfo( EventPlayer(), CameraInfoDefault(), 2.0, -1, 10, false );
	return true;
}

bool libcrtx_command_play(bool testConds, bool runActions) {
	string val;
	int flags = 0;
	unitgroup g = libcrtx_utility_getselectedunits();
	val = StringWord( EventChatMessage(false), 2 );
	val = StringCase(StringSub(val, 1, 1), true) + StringSub(val, 2, StringLength(val));

	if( StringEqual(StringWord( EventChatMessage(false), 3), "loop", false) ) {
		flags = c_animFlagPlayForever;
	}

	UnitGroupLoopBegin(g);
	while(!UnitGroupLoopDone())
	{
		if( StringEqual(val, "reset", false) ) {
			libNtve_gf_ClearAnimation( libNtve_gf_MainActorofUnit(UnitGroupLoopCurrent()), "libcrtx_command_play" );
		} else {
			libNtve_gf_PlayAnimation( libNtve_gf_MainActorofUnit(UnitGroupLoopCurrent()), "libcrtx_command_play", val, flags, 1 );
		}
		UnitGroupLoopStep();
	}
	UnitGroupLoopEnd();
	return true;
}














