// Cortex SC2 Roleplaying Engine
// Copyright (C) 2009-2011 <http://www.cortexrp.com/>
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; version 2 of the License.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// Some reserved variable names.
const string libcrtx_variable_name_last_spawned = "LastSpawned";
const string libcrtx_variable_name_original_selection = "Selection";
const string libcrtx_variable_name_all_spawned = "AllSpawned";
const string libcrtx_variable_name_return = "ReturnValue"; // This variable is used for internal stuff.

// Supported variable types
const int libcrtx_variable_type_none = -1;
const int libcrtx_variable_type_int = 0;
const int libcrtx_variable_type_fixed = 1;
const int libcrtx_variable_type_unitgroup = 2;
const int libcrtx_variable_type_string = 3;

// Builds the internal data-table storage name for a variable owned by a player with a specific name.
static string libcrtx_variable_get_storage_name(int player, string name)
{
	return "variable_" + IntToString(player) + "_" + StringCase(name, false);
}

// Marks a variable as having a specific type.
static void libcrtx_variable_assign_type(int player, string name, int type)
{
	DataTableSetInt( true, libcrtx_variable_get_storage_name(player, name) + "_type", type);
}

// Marks a variable as erased. This does not actually affect any memory, since Galaxy is GC'd.
void libcrtx_variable_delete(int player, string name)
{
	libcrtx_variable_assign_type(player, name, libcrtx_variable_type_none);
}

// Returns whether or not a variable exists, owned by the correct player, and having the correct type.
bool libcrtx_variable_exists(int player, string name, int type)
{
	if( !DataTableValueExists(true, libcrtx_variable_get_storage_name(player, name) + "_type") ) {
		return false;
	}

	if( DataTableGetInt( true, libcrtx_variable_get_storage_name(player, name) + "_type" ) == type ) {
		return true;
	}

	return false;
}

// Returns the type of the variable
int libcrtx_variable_get_type(int player, string name)
{
	return DataTableGetInt( true, libcrtx_variable_get_storage_name(player, name) + "_type");
}

// Assigns/Creates a variable to be of type unitgroup and have a value of unitgroup g. 
void libcrtx_variable_assign_group(int player, string name, unitgroup g)
{
	libcrtx_variable_assign_type( player, name, libcrtx_variable_type_unitgroup );
	DataTableSetUnitGroup( true, libcrtx_variable_get_storage_name(player, name), g);
}

// Retrieves a variable of type group, with name name.
unitgroup libcrtx_variable_get_group(int player, string name)
{
	return DataTableGetUnitGroup( true, libcrtx_variable_get_storage_name(player, name));
}

// Assigns/Creates a variable to be of type string and have a value of string s. 
void libcrtx_variable_assign_string(int player, string name, string s)
{
	libcrtx_variable_assign_type( player, name, libcrtx_variable_type_string );
	DataTableSetString( true, libcrtx_variable_get_storage_name(player, name), s);
}

// Retrieves a variable of type string, with name name.
string libcrtx_variable_get_string(int player, string name)
{
	return DataTableGetString( true, libcrtx_variable_get_storage_name(player, name));
}

// Assigns/Creates a variable to be of type fixed and have a value of fixed f. 
void libcrtx_variable_assign_fixed(int player, string name, fixed f)
{
	libcrtx_variable_assign_type( player, name, libcrtx_variable_type_fixed );
	DataTableSetFixed( true, libcrtx_variable_get_storage_name(player, name), f);
}

// Retrieves a variable of type fixed, with name name.
fixed libcrtx_variable_get_fixed(int player, string name)
{
	return DataTableGetFixed( true, libcrtx_variable_get_storage_name(player, name));
}


// Assigns/Creates a variable to be of type int and have a value of int i. 
void libcrtx_variable_assign_int(int player, string name, int i)
{
	libcrtx_variable_assign_type( player, name, libcrtx_variable_type_int );
	DataTableSetInt( true, libcrtx_variable_get_storage_name(player, name), i);
}

// Retrieves a variable of type int, with name name.
int libcrtx_variable_get_int(int player, string name)
{
	return DataTableGetInt( true, libcrtx_variable_get_storage_name(player, name));
}
